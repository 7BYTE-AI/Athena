//////////////////////// Athena common buffers ////////////////////////

layout(std140, set = 1, binding = 0) uniform u_CameraData
{
	mat4 View;
    mat4 InverseView;
    mat4 Projection;
    mat4 InverseProjection;
    mat4 ViewProjection;
    mat4 InverseViewProjection;
    mat4 RotationView;
    vec3 Position;
    float NearClip;
    float FarClip;
} u_Camera;

layout(std140, set = 1, binding = 1) uniform u_RendererData
{
    vec2 ViewportSize;
    vec2 InverseViewportSize;
    ivec4 ViewportTilesCount;  // xy - per width and height, z - all tiles, w - empty 
    float EnvironmentIntensity;
    float EnvironmentLOD;
    int DebugShadowCascades;
    int DebugLightComplexity;
} u_Renderer;

struct DirectionalLight
{
    vec4 Color;
    vec3 Direction;
    float Intensity;
    float LightSize;
	int CastShadows;
};

struct PointLight
{
    vec4 Color;
    vec3 Position;
    float Intensity;
    float Radius;
    float FallOff;
};

struct SpotLight
{
	vec4 Color;
	vec3 Position;
	float Intensity;
	vec3 Direction;
	float SpotAngle;    // Cosine of the half angle
    float InnerFallOff;
	float Range;
	float RangeFallOff;
};

layout(std430, set = 1, binding = 2) readonly buffer u_LightData
{
    DirectionalLight g_DirectionalLights[MAX_DIRECTIONAL_LIGHT_COUNT];
    int g_DirectionalLightCount;

    PointLight g_PointLights[MAX_POINT_LIGHT_COUNT];
    int g_PointLightCount;

    SpotLight g_SpotLights[MAX_SPOT_LIGHT_COUNT];
    int g_SpotLightCount;
};

struct TileVisibleLights
{
	uint LightCount;
	uint LightIndices[MAX_POINT_LIGHT_COUNT_PER_TILE];
};

layout(std430, set = 1, binding = 3) buffer u_VisibleLightsData
{
    TileVisibleLights u_VisibleLights[];
};

layout(std430, set = 1, binding = 4) readonly buffer u_BonesData
{
    mat4 g_Bones[];
};

mat4 GetBonesTransform(uint bonesOffset, ivec4 boneIDs, vec4 weights)
{
    mat4 bonesTransform = g_Bones[bonesOffset + boneIDs[0]] * weights[0];
    for(int i = 1; i < MAX_NUM_BONES_PER_VERTEX; ++i)
    {
        bonesTransform += g_Bones[bonesOffset + boneIDs[i]] * weights[i];
    }

    return bonesTransform;
}
