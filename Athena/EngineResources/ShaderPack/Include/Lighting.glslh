
#include "Buffers.glslh"
#include "PBR.glslh"
#include "Shadows.glslh"


struct DirectionalLight
{
    vec4 Color;
    vec3 Direction;
    float Intensity;
    float LightSize;
	int CastShadows;
};

struct PointLight
{
    vec4 Color;
    vec3 Position;
    float Intensity;
    float Radius;
    float FallOff;
};

struct SpotLight
{
	vec4 Color;
	vec3 Position;
	float Intensity;
	vec3 Direction;
	float SpotAngle;    // Cosine of the half angle
    float InnerFallOff;
	float Range;
	float RangeFallOff;
};

layout(std430, set = 1, binding = 2) readonly buffer u_LightData
{
    DirectionalLight g_DirectionalLights[MAX_DIRECTIONAL_LIGHT_COUNT];
    int g_DirectionalLightCount;

    PointLight g_PointLights[MAX_POINT_LIGHT_COUNT];
    int g_PointLightCount;

    SpotLight g_SpotLights[MAX_SPOT_LIGHT_COUNT];
    int g_SpotLightCount;
};

struct TileVisibleLights
{
	uint LightCount;
	uint LightIndices[MAX_POINT_LIGHT_COUNT_PER_TILE];
};

layout(std430, set = 1, binding = 3) buffer u_VisibleLightsData
{
    TileVisibleLights u_VisibleLights[];
};

layout(set = 1, binding = 14) uniform sampler2D u_BRDF_LUT;
layout(set = 1, binding = 15) uniform samplerCube u_EnvironmentMap;
layout(set = 1, binding = 16) uniform samplerCube u_IrradianceMap;


vec3 LightContribution(vec3 lightDirection, vec3 lightRadiance, vec3 normal, vec3 viewDir, vec3 albedo, float metalness, float roughness)
{
    vec3 reflectivityAtZeroIncidence = vec3(0.04);
    reflectivityAtZeroIncidence = mix(reflectivityAtZeroIncidence, albedo, metalness);

    vec3 negativeLightDir = -lightDirection;
    vec3 halfWayVector = normalize(viewDir + negativeLightDir);

    float NDF = DistributionGGX(normal, halfWayVector, roughness);
    float G = GeometrySmith(normal, viewDir, negativeLightDir, roughness);
    vec3 F = FresnelSchlick(max(dot(halfWayVector, viewDir), 0.0), reflectivityAtZeroIncidence);

    vec3 numerator = NDF * G * F;
    float denominator = 4.0 * max(dot(normal, viewDir), 0.0) * max(dot(normal, negativeLightDir), 0.0) + 0.0001;
    vec3 specular = numerator / denominator;

    float normalDotLightDir = max(dot(normal, negativeLightDir), 0.0);

    vec3 reflectedLight = F;
    vec3 absorbedLight = vec3(1.0) - reflectedLight;
        
    absorbedLight *= 1.0 - metalness;

    return (absorbedLight * albedo / PI + specular) * lightRadiance * normalDotLightDir;
}

vec3 ComputeDirectionalLightRadiance(DirectionalLight light)
{
    vec3 radiance = light.Color.rgb * light.Intensity;
    return radiance;
}

vec3 ComputePointLightRadiance(PointLight light, vec3 worldPos)
{
    float dist = length(worldPos - light.Position);

    if(dist >= light.Radius)
        return vec3(0.0);

    float attenuationFactor = dist / light.Radius;

    float factor2 = attenuationFactor * attenuationFactor;
    float attenuation = (1.0 - factor2) * (1.0 - factor2) / (1 + light.FallOff * factor2);
    attenuation = clamp(attenuation, 0.0, 1.0);

    vec3 radiance = light.Color.rgb * light.Intensity * attenuation;
    return radiance;
}

vec3 ComputeSpotLightRadiance(SpotLight light, vec3 worldPos)
{
    float dist = length(worldPos - light.Position);

    if(dist >= light.Range)
        return vec3(0.0);

    vec3 lightToPixel = normalize(worldPos - light.Position);
    float theta = dot(lightToPixel, light.Direction);

    if(theta < light.SpotAngle)
        return vec3(0.0);

    float distFactor = dist / light.Range;

    float distFactor2 = distFactor * distFactor;
    float rangeAttenuation = (1.0 - distFactor2) * (1.0 - distFactor2) / (1 + light.RangeFallOff * distFactor2);
    rangeAttenuation = clamp(rangeAttenuation, 0.0, 1.0);
    
    float thetaFactor = 1.0 - (theta - light.SpotAngle) / (1.0 - light.SpotAngle);

    float thetaFactor2 = thetaFactor * thetaFactor;
    float innerAttenuation = (1.0 - thetaFactor2) * (1.0 - thetaFactor2) / (1 + light.InnerFallOff * thetaFactor2);
    innerAttenuation = clamp(innerAttenuation, 0.0, 1.0);

    float attenuation = rangeAttenuation * innerAttenuation;

    vec3 radiance = light.Color.rgb * light.Intensity * attenuation;
    return radiance;
}

TileVisibleLights GetTileData(vec2 screenUV)
{
    ivec2 tileID = ivec2(screenUV / LIGHT_TILE_SIZE);
	uint tileIndex = tileID.y * u_Renderer.ViewportTilesCount.x + tileID.x;
    return u_VisibleLights[tileIndex];
}

vec3 GetLightComplexityDebugColor(uint count)
{
    if(count < 2)
        return vec3(0, 0.2, 1);
    if(count < 3)
        return vec3(0, 1, 1);
    if(count <= 4)
        return vec3(0, 1, 0);
    if(count <= 8)
        return vec3(1, 1, 0);
    if(count <= 12)
        return vec3(1, 0.4, 0);
    if(count < MAX_POINT_LIGHT_COUNT_PER_TILE)
        return vec3(1.0, 0.05, 0);

    return vec3(0, 0, 0);
}

vec3 GetDirectionalLight(vec3 normal, vec3 albedo, float roughness, float metalness, vec3 worldPos, vec3 viewDir, vec2 screenUV)
{
    float distanceFromCamera = distance(u_Camera.Position, worldPos);
    vec3 totalIrradiance = vec3(0.0);
    
    for (int i = 0; i < g_DirectionalLightCount; ++i)
    {
        DirectionalLight light = g_DirectionalLights[i];
        vec3 dir = light.Direction;
        vec3 radiance = ComputeDirectionalLightRadiance(light);
        
        float shadow = 0.0;
        if(bool(light.CastShadows))
            shadow = ComputeDirectionalLightShadow(light.LightSize, dir, normal, worldPos, distanceFromCamera, u_Camera.View, screenUV);
        
        if(shadow < 1.0)
            totalIrradiance += (1 - shadow) * LightContribution(dir, radiance, normal, viewDir, albedo.rgb, metalness, roughness);
    }
    

#if 0
    for (int j = 0; j < g_PointLightCount; ++j)
    {
        PointLight light = g_PointLights[j];
#else
    TileVisibleLights tileData = GetTileData(screenUV);

    for (int j = 0; j < tileData.LightCount; ++j)
    {
        uint lightIndex = tileData.LightIndices[j];
        PointLight light = g_PointLights[lightIndex];
#endif
        vec3 radiance = ComputePointLightRadiance(light, worldPos);
        
        if(radiance != vec3(0.0))
        {
            vec3 dir = normalize(worldPos - light.Position);
            totalIrradiance += LightContribution(dir, radiance, normal, viewDir, albedo.rgb, metalness, roughness);
        }
    }

    for (int j = 0; j < g_SpotLightCount; ++j)
    {
        SpotLight light = g_SpotLights[j];
        vec3 radiance = ComputeSpotLightRadiance(light, worldPos);
        
        if(radiance != vec3(0.0))
        {
            vec3 dir = normalize(worldPos - light.Position);
            totalIrradiance += LightContribution(dir, radiance, normal, viewDir, albedo.rgb, metalness, roughness);
        }
    }

    return totalIrradiance;
}

vec3 GetAmbientLight(vec3 normal, vec3 albedo, float metalness, float roughness, vec3 viewDir)
{    
    vec3 reflectivityAtZeroIncidence = vec3(0.04);
    reflectivityAtZeroIncidence = mix(reflectivityAtZeroIncidence, albedo, metalness);

    float NdotV = max(dot(normal, viewDir), 0.0);

    vec3 reflectedLight = FresnelSchlickRoughness(NdotV, reflectivityAtZeroIncidence, roughness); 
    vec3 absorbedLight = 1.0 - reflectedLight;
    absorbedLight *= 1.0 - metalness;

    vec3 irradiance = texture(u_IrradianceMap, normal).rgb;
    vec3 diffuseIBL = absorbedLight * irradiance;

    vec3 reflectedVec = reflect(-viewDir, normal); 

    float lod = u_Renderer.EnvironmentLOD + roughness * (MAX_SKYBOX_MAP_LOD - u_Renderer.EnvironmentLOD);
    vec3 envMapReflectedColor = textureLod(u_EnvironmentMap, reflectedVec, lod).rgb;  
    vec2 envBRDF = texture(u_BRDF_LUT, vec2(NdotV, roughness)).rg;
    vec3 specularIBL = envMapReflectedColor * (reflectedLight * envBRDF.x + envBRDF.y);

    vec3 ambient = (diffuseIBL * albedo + specularIBL) * u_Renderer.EnvironmentIntensity;
    return ambient;
}
